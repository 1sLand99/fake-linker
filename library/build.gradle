/*
 * Copyright (c) 2021 fake-linker by sanfengAndroid.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id 'com.android.library'
}

android {
    compileSdkVersion rootProject.targetSdk
    buildToolsVersion '33.0.0'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        externalNativeBuild {
            cmake {
                abiFilters.addAll(rootProject.abis)
                arguments '-DANDROID_PLATFORM=21',
                        "-DLINKER_MODULE_NAME=${rootProject.fakeLinkerModuleName}",
                        "-DINSTALLER_MODULE_NAME=${rootProject.installerModuleName}",
                        "-DHOOK_LOG_LEVEL=${rootProject.logLevel}",
                        "-DMODULE_VERSION=${rootProject.versionCode}",
                        "-DMODULE_VERSION_NAME=\\\"${rootProject.versionName}\\\"",
                        '-DFAKELINKER_TEST=ON'
            }
        }

        buildConfigField 'String', 'LINKER_MODULE_NAME', "\"${rootProject.fakeLinkerModuleName}\""
        buildConfigField 'String', 'HOOK_INSTALL_MODULE_NAME', "\"${rootProject.installerModuleName}\""
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'consumer-rules.pro'
            externalNativeBuild {
                cmake {
                    cppFlags "-fvisibility=hidden -fvisibility-inlines-hidden -Os -fomit-frame-pointer"
                    cFlags "-fvisibility=hidden -fvisibility-inlines-hidden -Os -fomit-frame-pointer"
                }
            }
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    ndkVersion '25.1.8937393'
    namespace 'com.sanfengandroid.fakelinker'

    buildFeatures{
        prefabPublishing true
    }
    prefab{
        fakelinker{
            headers "src/main/cpp/export"
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.startsWith('merge') && task.name.endsWith('NativeLibs')) {
        task.doLast {
            merge32BitTo64BitLibrary(it.outputs.files, rootProject.fakeLinkerModuleName)
            emulatorBuildMoveLibrary(it.outputs.files, rootProject.fakeLinkerModuleName)
            reducedLibrary(it.outputs.files)
        }
    }
}

dependencies {
}