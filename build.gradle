/*
 * Copyright (c) 2020 XpFilter by beich.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.library'
}

def buildApi = rootProject.ext.buildApi
def linkerModuleName = 'fake-linker'
def hookInstallModuleName = 'hook-install'
def mVersionCode = 1000
def mVersionName = "fake-linker-1.0"

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode mVersionCode
        versionName mVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        externalNativeBuild {
            cmake {
                cppFlags ""
                cFlags ""
                abiFilters 'armeabi-v7a', 'x86', 'arm64-v8a', 'x86_64'
                arguments "-DANDROID_PLATFORM=${buildApi}", "-DLINKER_MODULE_NAME=${linkerModuleName}",
                        "-DHOOK_INSTALL_MODULE_NAME=${hookInstallModuleName}",
                        "-DHOOK_LOG_LEVEL=${rootProject.ext.logLevel}",
                        "-DMODULE_VERSION=${mVersionCode}",
                        "-DMODULE_VERSION_NAME=\"${mVersionName}\""

            }
        }

        buildConfigField 'String', 'LINKER_MODULE_NAME', "\"${linkerModuleName}\""
        buildConfigField 'String', 'HOOK_INSTALL_MODULE_NAME', "\"${hookInstallModuleName}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets{
        release{
            jniLibs.srcDirs = ["src/main/libs/release"]
        }
        debug{
            jniLibs.srcDirs = ["src/main/libs/debug"]
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    ndkVersion '22.0.7026061'
}

tasks.whenTaskAdded { task ->
    if (task.name.startsWith('merge') && task.name.endsWith('JniLibFolders')) {
        task.doLast {
            it.outputs.files.files.each { File dir ->
                dir.eachFileRecurse {
                    if (it.name == "lib${linkerModuleName}-${buildApi}.so") {
                        println '找到当前正在编译的fake-linker模块版本,需要删除'
                        it.delete()
                    }
                }
            }
        }
    }
}
dependencies {

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    testImplementation 'junit:junit:4.13.1'
    implementation 'eu.chainfire:libsuperuser:1.0.0.201811281328'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}